DISTRO = "ostro"
DISTRO_NAME = "Ostro OS"
DISTRO_VERSION = "1.0~M1~snapshot-${DATE}"
SDK_VENDOR = "-ostrosdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"

MAINTAINER = "Ostro Dev <ostro-dev@lists.ostroproject.org>"

TARGET_VENDOR = "-ostro"

require conf/distro/include/ostro_security_flags.inc

OSTRO_DEFAULT_DISTRO_FEATURES = "systemd bluez5 pam"

# Remove currently unsupported distro features from global defaults
DISTRO_FEATURES_DEFAULT_remove = "x11 3g"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${DISTRO_FEATURES_LIBC} ${OSTRO_DEFAULT_DISTRO_FEATURES}"
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = ""
DISTRO_FEATURES_BACKFILL_CONSIDERED = "sysvinit"

# Smack security is a distribution feature which can be enabled or
# disabled as needed. To simplify recipes, there is also a smack
# override.
#
# At the moment, both must be kept in sync manually. In other words,
# to disable the default in a local.conf, change two variables
# like this:
#  OSTRO_SMACK_OVERRIDE = ""
#  OSTRO_DEFAULT_DISTRO_FEATURES_remove = "smack"
OSTRO_DEFAULT_DISTRO_FEATURES += "smack"
OSTRO_SMACK_OVERRIDE ?= ":smack"
DISTROOVERRIDES .= "${OSTRO_SMACK_OVERRIDE}"


# Set preferred version to 3.19% for all linux-yocto exept quark which
# is 4.0%.
PREFERRED_VERSION_linux-yocto ?= "3.19%"
PREFERRED_VERSION_linux-yocto_quark = "4.0%"

OS_RELEASE_FIELDS_append = " BUILD_ID"
